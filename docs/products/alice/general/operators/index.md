---
title: 演算子と式
summary: 式は、AliceScriptにおけるもっとも重要かつ基本的な要素です。
date : 2021-12-25
---

AliceScriptにおいて、ほとんどのものは==**式**==で記述されます。
AliceScriptでは式を、値を返すものすべてと定義します。
ここでは式を値があるものすべてと定義します。

AliceScriptにおける式のもっとも単純な形を次に示します。

```cs title="AliceScript"
1 + 1;
```

数学と同じですね。
この例では、`1+1`の計算を行っています。ここで、`1`は、明らかに`1`という値です。言い換えると`1`は`1`という値を表す式なのです。 このようにコード中に直接書かれた値のことを、==リテラル==と呼びます。

式の多くの使い道は、式の結果をどこかへ代入することです。
次の例をご覧ください。

```cs title="AliceScript"
var a = 1 + 1;
```

この例では、先ほどの計算結果を変数`a`に定義して代入しています。
この代入の後、変数`a`は、`2`になります。
このように何かの値に代入すると、==[代入文](./statement.md)==となります。
AliceScriptで変数を定義したり代入する方法について詳しく知るには、[変数](./variable.md)を参照してください。

ここでの`+`は加算演算子という演算子です。
AliceScriptにはさまざまな演算子が存在し、それらは以下のグループに大別できます。

- [算術演算子](./arithmetic-operators.md)
- [関係演算子](./relational-operators.md)
- 論理演算子
- ビット演算子
- 等値演算子
- 型演算子

### 複合代入
多くの算術演算子は、複合代入をサポートします。算術演算子を`op`と置いた場合、次のふたつの式は等価です。

```cs title="AliceScript"
x op= y;
```

```cs title="AliceScript"
x = x op y;
```

## 文字列連結
一方または両方の値が文字列型の場合、+演算子によってその値の文字列表現が連結されます。次に例を示します。

```cs title="JavaScript"
print("Hello,"+"World.");
//出力:Hello,World.
```

## 配列の連結または追加
左辺の変数が配列型の場合、右辺が配列型ならその配列が連結され、それ以外の型なら左辺の配列に右辺の項目が追加されます。次に例を示します。

```cs title="AliceScript"
var arr = ["a","b"];
print(arr);//出力:a,b
arr += "c";
print(arr);//出力:a,b,c
```

## デリゲートの組み合わせ
左辺と右辺の両方がデリゲート型の場合、左辺のデリゲートと右辺のデリゲートが結合された新しいデリゲートが返されます。次に例を示します。

```cs title="AliceScript"
var del1 = ()=>{
  print("Hello");
};
var del2 = ()=>{
  print("World");
};

var del = del1 + del2;
del.Invoke();
//出力:Hello
//    World
```

## ラムダ演算子
ラムダ演算子(`=>``)は、ラムダ式中で、左側の引数指定部と右側の式本体をわけるために使用します。[ラムダ式](../delegate)を参照してください。


## スプレッド演算子
<span class="badge bg-success">対応バージョン>=Alice3.0</span>

スプレッド演算子(`...`)は、コレクション式中で別の配列の中身を展開するときに使用します。
次の例を参照してください。

```cs title="AliceScript"
var baseArray = [1,2,3];

var combined = [0, ...baseArray , 4];

print(combined);//出力: 0,1,2,3,4
```


## 論理演算子
次の演算子は、ブール型の値について論理演算を行います。
### 論理否定演算子
論理否定演算子`!`は、単項の接頭辞でその値の論理否定が計算されます。次に例を示します。

```cs title="AliceScript"
print(!true);//出力:false
```

### 論理積演算子
論理積(論理AND)演算子`&`または`&&`は、左右の値の両方が`true`と評価された場合のみ`true`を返し、それ以外の場合`false`を返します。`&`演算子と`&&`演算子との唯一の違いは後者は左辺がfalseと評価された場合右辺の評価は行われないことです。次に例を示します。

```cs title="AliceScript"
print(true&true); //出力:true
print(true&false);//出力:false
```

### 論理和演算子
論理和(論理OR)演算子`|`または`||`は、左右の値の少なくとも一方が`true`と評価された場合に`true`を返し、それ以外の場合に`false`を返します。`|`演算子と`||`演算子との唯一の違いは後者は左辺がtrueと評価された場合右辺の評価は行われないことです。次に例を示します。

```cs title="AliceScript"
print(true|false); //出力:true
print(false|false);//出力:false
```

### 排他的論理和演算子
排他的論理和(論理XOR)演算子`^`は、左右の値が異なると評価された場合(すなわち、片方が`true`と評価されたならばもう片方が`false`と評価される場合)に`true`を返し、それ以外の場合はfalseを返します。次に例を示します。

```cs title="AliceScript"
print(true^false);//出力:true
print(true^true); //出力:false
```

## Null合体演算子
Null合体演算子は、左の値が`null`でない場合その値を返し、それ以外の場合は右の値が評価され返されます。次に例を示します。

```cs title="AliceScript"
var v = "ABC";
print(v??"Hello,World");//出力:ABC
v = null;
print(v??"Hello,World");//出力:Hello,World
```

## 型一致演算子
型一致演算子`is`は、左辺の値の型が右辺の値の表す型と一致するか、右辺の値の型と一致する場合は`true`を、それ以外の場合は`false`を返します。次に例を示します。

```cs title="AliceScript"
string str = "Hello,World";
if(str is string)
{
  print(str);
}
//出力:Hello,World
```

また、型が一致しない場合に`true`を返す`is not`演算子もあります。
次に例を示します。

```cs title="AliceScript"
string str = "Hello,World";
if(str is not number)
{
  print(str);
}
//出力:Hello,World
```

## 型変換演算子
型変換演算子`as`は、左辺の値を右辺の表す型に変換します。右辺は[Typeオブジェクト](../api/alice/interpreter/type/index.md)である必要があり、型変換に失敗した場合は`null`を返します。次に例を示します。

```cs title="AliceScript"
number num = 12345;
print(num.type);//出力:NUMBER
var str = num as string;
print(str.type);//出力:STRING
```


## 条件演算子
条件演算子(三項条件演算子)`?:`は、論理式を評価し、論理式の評価結果(`true`または`false`)に応じて続くふたつの式のいずれかの結果を返します。まずは次の例を参照してください。

```cs title="AliceScript"
var todo = 現在の気温 > 30.0 ? "エアコンを入れる" : "エアコンを切る";
print("todo={0}",todo);
//出力例
//現在の気温が20℃の場合 : todo=エアコンを切る
//現在の気温が32℃の場合 : todo=エアコンを入れる
```

上記の例では、現在の気温が30℃以上の場合、todoに「エアコンを入れる」が代入されます。

この例のように、この条件演算子の構文は次のようになります。

```cs title="構文"
条件 ? 結果A : 結果B
```
条件式は、論理式で`true`または`false`のどちらかと評価される必要があります。条件が`true`と評価された場合は結果Aの式が評価され、条件が`false`と評価された場合は結果Bの式が評価され、それらの結果が演算の結果として返されます。結果Aと結果Bの両方が評価されることはありません。

AliceScriptRC1時点で、条件演算子に複数の条件演算が含まれる処理は実行できません。つまり、次の式は正しく評価することができません。

```cs title="AliceScript"
a ? b : c ? d : e
```

### 条件演算子とIf文
条件演算子を使用すると、いままでIf文を使用して行ってきたことの一部を簡潔な形で書き直すことができます。If文と条件演算子を状況に応じて使い分けると、よりコードが読みやすくなります。次の例を参照してください。

```cs title="AliceScript"
if(今日の天気 == 晴れ)　
  {
　  todo = "公園に行き";
  }
else
  {
　  todo = "家にい";
  }
print("今日は{0}ます。",todo);
//出力例
//今日の天気が晴れの場合 : 今日は、公園に行きます。
//今日の天気が曇りの場合 : 今日は、家にいます。
```

上記の例は、現在の天気が晴れであれば、todoに「公園に行き」が代入され、「今日は公園に行きます。」が表示されます。 それ以外の場合では、todoに「家にい」が代入され、「今日は家にいます。」が表示されます。

このコードを条件演算子を用いた書き方にすると次の例のようになります。

```cs title="AliceScript"
(今日の天気 == 晴れ) ? todo = "公園に行き" : todo = "家にい";
print("今日は、{0}ます。",todo);
```

これはただ単にIf文を条件演算子に書き換えただけで、あまりよい使用方法とは言えません。条件演算子の値を返すという特性を使用すれば、次の例のように書き換えることもできます。

```cs title="AliceScript"
todo = (今日の天気 == 晴れ) ? "公園に行き" : "家にい";
print("今日は、{0}ます。",todo);
```

上記の例のように左辺に対して条件演算の結果を代入したほうが、より簡潔かつ分かりやすくなります。条件演算子をまだよく理解していない人のためにコメントを書いておくとより良いでしょう。

条件演算子は行数を短くするよりも、他の人がコードを読んだときに簡単に理解できるようになることを期待して導入されました。基本的には、If文を使用し、条件演算子はそれを使用することで簡潔にわかりやすくなる場合のみ使用するべきです。
